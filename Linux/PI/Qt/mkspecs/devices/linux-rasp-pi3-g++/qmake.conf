#
# qmake configuration for Broadcom's Raspberry PI
# http://wiki.qt.io/RaspberryPi

include(../common/linux_device_pre.conf)

QMAKE_LFLAGS           += -Wl,-rpath-link,$$[QT_SYSROOT]/opt/vc/lib

QMAKE_LIBDIR_OPENGL_ES2 = /opt/Qt5Pi/sysroot/opt/vc/lib
QMAKE_LIBDIR_OPENGL	= /opt/Qt5Pi/sysroot/opt/vc/lib
QMAKE_LIBDIR_EGL        = $$QMAKE_LIBDIR_OPENGL_ES2
QMAKE_LIBDIR_OPENVG     = $$QMAKE_LIBDIR_OPENGL_ES2

QMAKE_INCDIR_EGL        = /opt/Qt5Pi/sysroot/opt/vc/include \
                          /opt/Qt5Pi/sysroot/opt/vc/include/interface/vcos/pthreads \
                          /opt/Qt5Pi/sysroot/opt/vc/include/interface/vmcs_host/linux
QMAKE_INCDIR_OPENGL_ES2 = $${QMAKE_INCDIR_EGL}
QMAKE_INCDIR_OPENVG     = $${QMAKE_INCDIR_EGL}
QMAKE_INCDIR_OPENGL	= $${QMAKE_INCDIR_EGL}

QMAKE_LIBS_OPENGL_ES2	= $${QMAKE_LIBS_OPENGL}

QMAKE_LIBS_OPENGL	= -lGLESv2
QMAKE_LIBS_EGL          = -lEGL -lGLESv2
QMAKE_LIBS_OPENVG       = -lEGL -lOpenVG -lGLESv2


contains(DISTRO, squeeze) {
    #Debian Squeeze: Legacy everything
    QMAKE_LIBS_OPENGL_ES2   = -lGLESv2 -lEGL
} else:contains(DISTRO, arch) {
    #On principle: no wizardry required
} else {
    #This is not strictly necessary
    DISTRO_OPTS += deb-multi-arch
    DISTRO_OPTS += hard-float
}

QMAKE_CFLAGS           = -march=armv8-a -mtune=cortex-a53 -mfpu=crypto-neon-fp-armv8
QMAKE_CXXFLAGS          = $$QMAKE_CFLAGS

# Preferred eglfs backend
EGLFS_DEVICE_INTEGRATION = eglfs_brcm

include(../common/linux_arm_device_post.conf)

load(qt_config)
